apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.1.0'
group 'com.bintray'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/shortcut-sdk-android-release.aar")
            groupId 'com.shortcutmedia.shortcut.sdk'
            artifactId 'shortcut-sdk'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'shortcut-sdk'
        name = 'ShortcutSDK'
        description = "This SDK provides components that allow you to interact with the Shortcut Image Recognition Service in your Android app."
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/shortcutmedia/shortcut-sdk-android.git'
        dryRun = false
        version {
            name = this.version
            desc = "ShortcutSDK Android ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}
